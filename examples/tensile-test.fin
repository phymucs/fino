MESH FILE_PATH tensile-test.msh  DIMENSIONS 3 # mesh file in Gmsh format

FINO_SOLVER PROGRESS_ASCII    # show a feedback in the terminal while solving

# uniform properties
E = 200e3   # [ MPa ] Young modulus ~ 200 GPa
nu = 0.3    # Poisson ratio

# boundary conditions ("left" and "right" come from the names in the mesh)
PHYSICAL_ENTITY left  BC fixed
PHYSICAL_ENTITY right BC tx=0.002*E*(10/13)  # traction

# other possible conditions:
# PHYSICAL_ENTITY right BC p=0.002*E*(10/13)   # pressure
# PHYSICAL_ENTITY right BC u=0.3               # displacement
# PHYSICAL_ENTITY right BC Mx=1                # moment

# print some data
PRINT TEXT "elements                    =" %g elements
PRINT TEXT "nodes                       =" %g nodes

FINO_STEP

# results
MESH_POST FILE_PATH tensile-test.vtk sigma sigma1 sigma2 sigma3 VECTOR u v w

# displ_max gets the norm of the maximum displacement
PRINT TEXT "maximum_displacement    [mm]  =" displ_max
# u(x,y,z) (and v(x,y,z) and z(x,y,y) are the individual displacements
PRINT TEXT "horizontal_displacement [mm]  =" u(80,0,0)
# sigma(x,y,z) is the von mises stress
PRINT TEXT "stress_at_the_origin    [MPa] =" sigma(0,0,0)
# the reaction force is stored at "physical entity name underscore RF"
PRINT TEXT "reaction_at_fixed_end   [N]   =" left_RF

PRINT FILE_PATH tensile-sigma.dat %.0f sigma(0,0,0)
