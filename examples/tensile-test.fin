MESH FILE_PATH tensile-test-specimen2.msh   # mesh file in Gmsh format
# the file tensile-test-specimen1.msh has the same grid but with first-order elements

# uniform properties
E = 200e3   # [ MPa ] Young modulus ~ 200 GPa
nu = 0.3    # Poisson ratio

# boundary conditions (the names come from the physical entities in the mesh)
PHYSICAL_ENTITY NAME left  BC fixed
# PHYSICAL_ENTITY NAME right BC tx=0.002*E*(10/13)  # traction
# PHYSICAL_ENTITY NAME right BC p=0.002*E*(10/13)   # pressure
PHYSICAL_ENTITY NAME right BC u=0.3 v=0 w=0  # displacement

# ask Fino to compute the reaction at left as R_x R_y and R_z
FINO_REACTION PHYSICAL_ENTITY left NAME_ROOT R

# print some data
PRINT TEXT "elements                    =" %g elements
PRINT TEXT "nodes                       =" %g nodes

FINO_STEP

# FINO_DEBUG MATRICES_X

# results
MESH_POST FILE_PATH tensile-test.vtk sigma sigma1 sigma2 sigma3 VECTOR u v w

PRINT TEXT "maximum_diplacement   [mm]  =" %g displ_max
PRINT TEXT "displacement at right [mm]  =" %g u(80,0,0)
PRINT TEXT "stress_at_the_origin  [MPa] =" %g sigma(0,0,0)
PRINT TEXT "reaction_at_fixed_end [N]   =" %g TEXT "Rx ="  R_x TEXT "Ry =" R_y TEXT "Rz =" R_z
PRINT TEXT "iterations                  =" %g fino_iterations
PRINT FILE_PATH tensile-sigma.dat %.0f sigma(0,0,0)
