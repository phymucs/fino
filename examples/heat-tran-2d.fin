# example of a 2D heat transient problem
MESH FILE_PATH square.msh DIMENSIONS 2
FINO_PROBLEM HEAT DIMENSIONS 2

end_time = 2000

# material properties
k = 1e-9        # thermal conduction
rho = 7800e-9   # density
cp = 0.49e3     # calorific capacity
h_conv = 0.003

# a linear piecewise temperature
FUNCTION Tin(t) INTERPOLATION linear DATA {
0    300
50   300
450  100
850  100
1250 300
1500 300
}


# the boundary conditions
delta = 100
PHYSICAL_GROUP NAME left      BC T=Tin(t)+delta*(0.5+y)
PHYSICAL_GROUP NAME right     BC T=Tin(t)
PHYSICAL_GROUP NAME bottom    BC T=Tin(t)
PHYSICAL_GROUP NAME top       BC T=Tin(t)+delta


FINO_STEP

PRINT t step_transient Tin(t) T(0,0) T_min T_max

MESH_POST FILE_PATH temp-square.msh T

IF done
 PRINT "\# open temp-anim-square.geo in Gmsh to create a quick rough video"
 PRINT "\# run  temp-anim-square.py  to get a nicer and smoother video"
ENDIF
